pkgname=cygwin-xz
pkgver=5.6.3
pkgrel=1
pkgdesc="Library and command line tools for XZ and LZMA compressed files (Cygwin)"
arch=('any')
url="https://tukaani.org/xz/"
license=('GPL' 'LGPL' 'custom')
groups=('cygwin-toolchain')
depends=('cygwin')
makedepends=('cygwin-configure')
conflicts=('cygwin-liblzma')
provides=('cygwin-liblzma')
options=('!strip' 'staticlibs' '!libtool' '!buildflags')
source=("https://github.com/tukaani-project/xz/releases/download/v${pkgver}/xz-${pkgver}.tar.gz")
sha256sums=('b1d45295d3f71f25a4c9101bd7c8d16cb56348bbef3bbc738da0351e17c73317')

_targets="x86_64-pc-cygwin"

build() {
  cd xz-${pkgver}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure
      make
    popd
  done
}

package() {
  cd xz-${pkgver}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g
    if [[ ! -d "$pkgdir"/usr/share/doc/${pkgname} ]]; then
      install -d "$pkgdir"/usr/share/doc/
      mv "$pkgdir"/usr/${_target}/share/doc/xz "$pkgdir"/usr/share/doc/${pkgname}/
    fi
    # remove unnecessary files
    find "$pkgdir"/usr/${_target}/bin/ ! -type d ! -name \*.dll -delete
    rm -rf "$pkgdir"/usr/${_target}/share
  done
  install -d -m0755 "$pkgdir"/usr/share/licenses/${pkgname}/
  ln -sf /usr/share/doc/${pkgname}/COPYING "$pkgdir"/usr/share/licenses/${pkgname}/
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
