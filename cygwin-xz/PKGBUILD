pkgname=cygwin-xz
pkgver=5.6.1
pkgrel=2
pkgdesc="Library and command line tools for XZ and LZMA compressed files (Cygwin)"
arch=('any')
url="https://xz.tukaani.org/xz-utils/"
license=('GPL' 'LGPL' 'custom')
groups=('cygwin-toolchain')
depends=('cygwin')
makedepends=('cygwin-configure' 'git' 'po4a' 'doxygen')
conflicts=('cygwin-liblzma')
provides=('cygwin-liblzma')
options=('!strip' 'staticlibs' '!libtool' '!buildflags')
source=("git+https://git.tukaani.org/xz.git#tag=v${pkgver}")
sha256sums=('SKIP')

_targets="x86_64-pc-cygwin"

prepare() {
  cd xz

  ./autogen.sh
}

build() {
  cd xz
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure
      make
    popd
  done
}

package() {
  cd xz
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g
    if [[ ! -d "$pkgdir"/usr/share/doc/${pkgname} ]]; then
      install -d "$pkgdir"/usr/share/doc/
      mv "$pkgdir"/usr/${_target}/share/doc/xz "$pkgdir"/usr/share/doc/${pkgname}/
    fi
    # remove unnecessary files
    find "$pkgdir"/usr/${_target}/bin/ ! -type d ! -name \*.dll -delete
    rm -rf "$pkgdir"/usr/${_target}/share
  done
  install -d -m0755 "$pkgdir"/usr/share/licenses/${pkgname}/
  ln -sf /usr/share/doc/${pkgname}/COPYING "$pkgdir"/usr/share/licenses/${pkgname}/
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
