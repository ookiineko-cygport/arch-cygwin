pkgname=cygwin-pcre2
pkgver=10.43
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions. 2nd version (Cygwin)"
arch=('any')
url="https://www.pcre.org/"
license=('BSD')
groups=('cygwin-toolchain')
depends=('cygwin')
makedepends=('cygwin-configure'
             'cygwin-readline' 'cygwin-zlib' 'cygwin-bzip2')
options=('!strip' 'staticlibs' '!libtool' '!buildflags' '!emptydirs')
source=("https://github.com/PCRE2Project/pcre2/releases/download/pcre2-${pkgver}/pcre2-${pkgver}.tar.bz2"
        cygwin-jit.patch)
sha256sums=('e2a53984ff0b07dfdb5ae4486bbb9b21cca8e7df2434096cc9bf1b728c350bcb'
            '1fd6d7ccfeaa2797e6c54e4410b7286da52c8a3c8f994632efe0070a3bfc0d7a')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/pcre2-${pkgver}

  patch -Np2 -i ../cygwin-jit.patch
}

build() {
  cd "$srcdir"/pcre2-${pkgver}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure \
        --enable-pcre2-16 \
        --enable-pcre2-32 \
        --enable-jit \
        --enable-pcre2grep-libz \
        --enable-pcre2grep-libbz2 \
        --enable-pcre2test-libreadline \
        --enable-newline-is-anycrlf \
        ac_cv_header_windows_h=no
      make
    popd
  done
}

package() {
  cd "$srcdir"/pcre2-${pkgver}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g
    # remove unnecessary files
    rm -r "$pkgdir"/usr/${_target}/share/{man,doc}/
  done
  install -Dm644 LICENCE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
