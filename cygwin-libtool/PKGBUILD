# NOTE: requires rebuilt with each new gcc version

pkgname=cygwin-libtool
pkgver=2.5.3
pkgrel=1
pkgdesc="A generic library support script (Cygwin)"
arch=('any')
url="https://www.gnu.org/software/libtool"
license=('GPL')
groups=('cygwin-toolchain')
depends=('sh' 'tar' 'cygwin')
makedepends=('cygwin-configure' 'help2man')
conflicts=('cygwin-libltdl')
provides=('cygwin-libltdl')
options=('!strip' 'staticlibs' '!libtool' '!buildflags')
source=("https://ftp.gnu.org/gnu/libtool/libtool-${pkgver}.tar.xz"
        cygming-pass-ld-flags.patch
        cygming-fix-seems-to-be-moved.patch
        cygming-pick-up-clang-rt.patch)
sha256sums=('898011232cc59b6b3bbbe321b60aba9db1ac11578ab61ed0df0299458146ae2e'
            '62c524e302421e7505995e6b4ce5e0d9d64a41a20645f7a4706472ccc009331f'
            'dc39fbe066958178f96108f07db62b48b9339efddf2c21f800ff8d67110ca393'
            '3b0a03a0210f015b633d429141100a2e7acd0bf81943af7c4976e7e06cac3f8b')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/libtool-${pkgver}
  patch -p2 -i ../cygming-pass-ld-flags.patch
  patch -p1 -i ../cygming-fix-seems-to-be-moved.patch
  patch -p2 -i ../cygming-pick-up-clang-rt.patch
}

build() {
  cd "$srcdir"/libtool-${pkgver}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure \
    lt_cv_sys_lib_dlsearch_path_spec="/usr/${_target}/lib"
    make
    popd
  done
}

package() {
  cd "$srcdir"/libtool-${pkgver}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    pushd build-${_target}
    make DESTDIR="$pkgdir" install
    ${_target}-strip --strip-unneeded "$pkgdir"/usr/${_target}/bin/*.dll
    ${_target}-strip -g "$pkgdir"/usr/${_target}/lib/*.a

    # remove unnecessary files
    rm -rf "$pkgdir"/usr/${_target}/share/man

    # remove dir file
    rm -rf "$pkgdir"/usr/${_target}/share/info/dir
    popd
  done
}
