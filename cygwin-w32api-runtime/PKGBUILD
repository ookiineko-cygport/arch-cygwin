pkgname=cygwin-w32api-runtime
pkgver=12.0.0
pkgrel=1
pkgdesc="Windows API libraries for the Cygwin cross toolchain"
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=('cygwin-toolchain')
makedepends=('cygwin-gcc' 'cygwin-binutils' "cygwin-w32api-headers=${pkgver}")
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2")
sha256sums=('cc41898aac4b6e8dd5cffd7331b9d9515b912df4420a3a612b5ea2955bbeed2f')

_targets="x86_64-pc-cygwin"

build() {
  cd "$srcdir"
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    if [ ${_target} == "x86_64-pc-cygwin" ]; then
        _configure_args="--disable-lib32 --enable-lib64"
    fi
    mkdir -p "$srcdir"/build-${_target} && cd "$srcdir"/build-${_target}
    ../mingw-w64-v${pkgver}/mingw-w64-crt/configure --prefix=/usr/${_target} \
        --host=${_target} --enable-w32api \
        ${_configure_args}
    make
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    cd "$srcdir"/build-${_target}
    make DESTDIR="$pkgdir" install
  done
}
