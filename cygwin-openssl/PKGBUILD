pkgname=cygwin-openssl
pkgver=3.0.14
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (Cygwin)"
arch=('any')
url="https://www.openssl.org"
license=('Apache')
groups=('cygwin-toolchain')
depends=('cygwin' 'cygwin-zlib')
makedepends=('cygwin-gcc' 'cygwin-environment' 'perl')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://www.openssl.org/source/openssl-${pkgver}.tar.gz"
        "https://src.fedoraproject.org/rpms/openssl/raw/f38/f/0007-Add-support-for-PROFILE-SYSTEM-system-default-cipher.patch"
        "https://src.fedoraproject.org/rpms/openssl/raw/f38/f/0010-Add-changes-to-ectest-and-eccurve.patch"
        "https://src.fedoraproject.org/rpms/openssl/raw/f38/f/0011-Remove-EC-curves.patch"
        "https://src.fedoraproject.org/rpms/openssl/raw/f38/f/0012-Disable-explicit-ec.patch"
        "https://src.fedoraproject.org/rpms/openssl/raw/f38/f/0049-Allow-disabling-of-SHA1-signatures.patch"
        "https://src.fedoraproject.org/rpms/openssl/raw/f38/f/0052-Allow-SHA1-in-seclevel-1-if-rh-allow-sha1-signatures.patch"
        0051-Support-different-R_BITS-lengths-for-KBKDF.patch
        cygwin-0100-RSA-PKCS15-implicit-rejection.patch)
sha256sums=('eeca035d4dd4e84fc25846d952da6297484afa0650a6f84c682e39df3a4123ca'
            'deaaaa96ff1c3f629863ed12e45ea845802f5375d04023013774160a1e0ce7c1'
            '2dccbfbbcf7739cb79d9c9277af41c920bcc5b712e3160e3bc38a25dcf22a6e7'
            '6882968e5f7be245dd865e4eb3aa0d1e39798c4e9021bb6a7fad2eabe6378910'
            '1e44308272b41598ddd5a94d71db8c84b6c737501ca8b3538b09ca48123ab42e'
            'cb9c0c88b885777be3aff1d3d3885cae6699aff9cf4a4facf301929e79bf513c'
            '506aa1c5d0a308fc9c14eed0dd59e0f72c5b3ab1c9342225e1decb9615ad6c07'
            '6aa04334e4f0a1157712edec86b6b22fd198ba83c016837874da6cc8c999b99e'
            '09bf5f5d191afb4dd830e1cb3204a4c9e2c5de1b8530d29536a94602a0f6c612')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/openssl-${pkgver}

  # remove files created by patch
  rm -f test/recipes/30-test_evp_data/evpkdf_kbkdf_counter.txt

  patch -Np1 -i ../0007-Add-support-for-PROFILE-SYSTEM-system-default-cipher.patch
  patch -Np0 -i ../0010-Add-changes-to-ectest-and-eccurve.patch
  patch -Np0 -i ../0011-Remove-EC-curves.patch
  patch -Np1 -i ../0012-Disable-explicit-ec.patch
  patch -Np1 -i ../0049-Allow-disabling-of-SHA1-signatures.patch
  patch -Np1 -i ../0051-Support-different-R_BITS-lengths-for-KBKDF.patch
  patch -Np1 -i ../0052-Allow-SHA1-in-seclevel-1-if-rh-allow-sha1-signatures.patch
  patch -Np1 -i ../cygwin-0100-RSA-PKCS15-implicit-rejection.patch
}

build() {
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    source cygwin-env ${_target}

    # conflict with --cross-compile-prefix
    unset CC CXX

    local configure_opts=()

    if [[ "${_target::-10}" == "x86_64" ]]; then
      configure_opts+=enable-ec_nistp_64_gcc_128
    fi

    rm -rf build-${_target}
    cp -r openssl-${pkgver} build-${_target}
    pushd build-${_target}
      ./Configure \
        --prefix=/usr/${_target} --cross-compile-prefix=${_target}- \
        --libdir=/usr/lib --openssldir=/etc/pki/tls \
        --system-ciphers-file=/etc/crypto-policies/back-ends/openssl.config \
        shared zlib enable-md2 enable-rc5 \
        enable-weak-ssl-ciphers enable-ssl3 enable-ssl3-method \
        no-mdc2 no-ec2m no-sm2 no-sm4 \
        "${configure_opts[@]}" Cygwin-${_target::-10} \
        "-DDEVRANDOM=\"\\\"/dev/urandom\\\"\" ${CFLAGS}"
      make
    popd
  done
}

package() {
  cd "$srcdir"/openssl-${pkgver}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd "$srcdir"/build-${_target} && make DESTDIR="$pkgdir" install_sw)
    mv "$pkgdir"/usr/lib/ "$pkgdir"/usr/${_target}/lib/
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g
    # fixup libdir in .pc files
    sed -i "s|/usr/lib|/usr/${_target}/lib|g" "$pkgdir"/usr/${_target}/lib/pkgconfig/*.pc
    # remove unnecessary files
    find "$pkgdir"/usr/${_target}/bin/ ! -type d ! -name \*.dll -delete
  done
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
