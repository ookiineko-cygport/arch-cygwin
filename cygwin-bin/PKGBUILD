_realname=cygwin
pkgname=cygwin-bin
pkgver=3.5.0
pkgrel=1
pkgdesc="POSIX compatibility C runtime for Windows (bootstrap)"
arch=('any')
url="https://cygwin.com/"
_mirror="https://mirrors.kernel.org/sourceware/cygwin"
license=('GPL')
provides=('cygwin')
conflicts=('cygwin')
options=('!strip' 'staticlibs' '!emptydirs')
_rev=1
source=("${_realname}-${pkgver}-${_rev}-x86_64.tar.xz::${_mirror}/x86_64/release/${_realname}/${_realname}-${pkgver}-${_rev}.tar.xz"
        "${_realname}-devel-${pkgver}-${_rev}-x86_64.tar.xz::${_mirror}/x86_64/release/${_realname}/${_realname}-devel/${_realname}-devel-${pkgver}-${_rev}.tar.xz")
noextract=("${source[@]%%::*}")
sha256sums=('989638f94e3642e7654657cb838ba7481365b5690c7f2ccf9ec5014a50401449'
            'cc846a259653b63c4f0718f6097cd449022a02d478185d72c5d1cf0c4cd80ffe')

_targets="x86_64-pc-cygwin"

package() {
  for _target in ${_targets}; do
    echo "Extracting ${_target} tarballs"
    mkdir -p "$pkgdir"/usr/${_target}
    tar -x -v -f "${_realname}-${pkgver}-${_rev}-${_target::-10}.tar.xz" -C "$pkgdir"/usr/${_target} --strip-components=1 --wildcards usr/\*
    tar -x -v -f "${_realname}-devel-${pkgver}-${_rev}-${_target::-10}.tar.xz" -C "$pkgdir"/usr/${_target} --strip-components=1 --wildcards usr/\*
    # install licenses
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/COPYING ]]; then
      install -Dm644 "$pkgdir"/usr/${_target}/share/doc/Cygwin/COPYING \
          "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
    fi
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/CYGWIN_LICENSE ]]; then
      install -Dm644 "$pkgdir"/usr/${_target}/share/doc/Cygwin/CYGWIN_LICENSE \
          "$pkgdir"/usr/share/licenses/${pkgname}/CYGWIN_LICENSE
    fi
    # remove unnecessary files
    rm -rf "$pkgdir"/usr/${_target}/share/doc/
    rm -rf "$pkgdir"/usr/${_target}/share/cygwin/
    find "$pkgdir"/usr/${_target}/bin/ -type f ! -name \*.dll -delete
    # fixup file modes
    find "$pkgdir"/usr/${_target}/ -mindepth 1 -type d -perm 775 -exec chmod 755 {} +
  done
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
