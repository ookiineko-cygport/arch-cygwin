pkgname=cygwin-wine
pkgver=1
pkgrel=1
pkgdesc="Wine wrapper for Cygwin"
arch=('any')
url="http://fedoraproject.org/wiki/MinGW"
license=('MIT')
groups=('cygwin-toolchain')
# https://bugs.archlinux.org/task/77413
depends=('wine' 'libunwind' 'cygwin')
source=(cygwin-wine.sh
        cygwin-winepath.sh)
install=cygwin-wine.install
md5sums=('SKIP' 'SKIP')

_targets="x86_64-pc-cygwin"

build() {
  for _target in ${_targets}; do
    sed "s|@TRIPLE@|${_target}|g" cygwin-wine.sh > ${_target}-wine
    sed "s|@TRIPLE@|${_target}|g" cygwin-winepath.sh > ${_target}-winepath
  done
}

package() {
  install -d "$pkgdir"/usr/bin
  for _target in ${_targets}; do
    install -m 755 ${_target}-wine "$pkgdir"/usr/bin/
    install -m 755 ${_target}-winepath "$pkgdir"/usr/bin/

    # fix tmpdir
    mkdir -p "$pkgdir"/usr/${_target}/
    ln -s /tmp/ "$pkgdir"/usr/${_target}/tmp
  done
}
